## Made by VideNoir - Secret of Life MUD on CoffeeMud v5.10.1
## This took 16 hours or something to get right for some reason.
##
##toggles console on CONSOLE OFF

once_prog
mpsetvar $i powerswitch 0
mpgset $i description '^W._____________________________________.%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|_______________________________[^R(|)^W]_|^N%0D ^cCONSOLE ON^N to turn it on.'
mpsavevar $i powerswitch
~

cnclmsg_prog huh p console off
if var($i powerswitch == 1)
mpecho $n presses the power button on $i
mpecho $i's fans stop whirring as it dies down.
mpsetvar $i powerswitch 0
mpsavevar $i powerswitch
mpgset $i description '^W._____________________________________.%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|                                     |%0D|_______________________________[^R(|)^W]_|^N%0D ^cCONSOLE ON^N to turn it on.'
else
mpechoat $n $i is already turned off.
endif
return cancel
~

##toggles console on CONSOLE ON
cnclmsg_prog huh p console on
if var($i powerswitch != 1)
mpecho $n presses the power button on $i
mpecho $i whirrs a little while powering up.
mpsetvar $i powerswitch 1
mpsavevar $i powerswitch
mpgset $i description '^G._____________________________________.%0D|                                     |%0D| Possible Commands:                  |%0D| Console Off                         |%0D| Checkin                             |%0D| Balance                             |%0D| Resign                              |%0D|                                     |%0D| ^BManagement Commands:^G                |%0D| ^B(*)Register  Employee^G               |%0D| ^B(*)Unregister Employee^G              |%0D|_______________________________^W[^g(|)^W]^G_|^N'
else
mpechoat $n $i is already turned on.
endif
return cancel
~

## Allows adding days of working and gives wage payment when reached 15 days of work CHECKIN
cnclmsg_prog huh p checkin
mploadvar $n workeddays
mploadvar $n lastworkday
mploadvar $n employeewage
if var($i powerswitch == 1)
    if affected('$n' 'Property_Neurallink')
        mpecho $n draws out $m personal jack from $m implant and jacks in the machine, processing $m ID and bio-signature...
        if islike($i '-TATTOOS +$n')
            if var($n workeddays <= 14)
            mpsetvar $n todaydate $%ISRLDAY()%
            ## $%ISRLDAY()% maybe
                if var($n todaydate != $<$n lastworkday>)
                    mpsetvar $n workeddays ++
                    mpsavevar $n workeddays
                    mpalarm 2 mpecho ^p$i pings ^w'^gYour checking in is now processed into your worked days.^2'^N%0D^p$i pings ^w'Accessing days worked since last payment... $<$n workeddays>/15, Optimal Credit payout: ^g$<$n employeewage>c^w. Have a productive day.'^N
                    mpsetvar $n lastworkday $%ISRLDAY()%
                    mpsavevar $n lastworkday
                else
                    mpalarm 2 mpecho ^p$i pings ^w'Accessing days worked since last payment... $<$n workeddays>/15, Optimal Credit payout: ^g$<$n employeewage>c^w. Have a productive day.'^N
                endif
            else
                mpalarm 2 mpecho ^p$i pings ^w'Accessing days worked since last payment... $<$n workeddays>/15. ^gCongratulations! You are eligible for wage payment.^w'^N
                if goldamt($i >= $<$n employeewage>)
                    mpforce $i drop $<$n employeewage>$
                    mpforce $n get $<$n employeewage>$ credits
                    mpsetvar $n workeddays 0
                    mpsavevar $n workeddays
                    mpsetvar $n lastworkday $%ISRLDAY()%
                    mpsavevar $n lastworkday
                else
                    mpalarm 2 mpecho ^p$i pings ^w'ERROR: Insufficient funds in terminal, contact management for refill.'^N
                endif
            endif
        else
            mpalarm 2 mpecho ^p$i pings ^w'ERROR: Bio-signature does not match any employee in the database.'^N
        endif
    else
        mpalarm 2 mpecho $n could not verify $m ID and biosignature due lacking a neural implant.
    endif
else
    mpechoat $n $i is not turned on!
endif
return cancel
~

# Adds a new employee REGISTER EMPLOYEE
cnclmsg_prog huh p register employee
if var($i powerswitch == 1)
    if islike($n -LOCATION +OWNED)
        mpprompt $n registereename Name of the Employee
        if cansee($i '$<$n registereename>')
            mpprompt $n wageamount Type a number for the wage in credits for $<$n registereename>. (You will have to give the console a budget of money.)
            if var($n wageamount < 0)
                mpsetvar $n wageamount 0
            endif
            mpsetvar '$<$n registereename>' employeewage $<$n wageamount>
            mpsavevar '$<$n registereename>' employeewage
            mptattoo $i '$<$n registereename>'
            mpecho $n types in some things to the terminal and registers $<$n registereename> as an employee with ^g$<$n wageamount>c^N wage.
        else
            mpecho ^p$i pings ^w'ERROR: Registeree not found in the room.'^N
        endif
    else
        if islike($n -LOCATION +CLANOWNED)
            if clanrank($n >= 5)
                mpprompt $n registereename Name of the Employee
                    if cansee($i '$<$n registereename>')
                        mploadvar '$<$n registereename>' workingsomewhere
                            mpprompt $n wageamount Type a number for the wage in credits for $<$n registereename>. (You will have to give the console a budget of money.)
                            mptattoo $i '$<$n registereename>'
                            mpecho $n types in some things to the terminal and registers $<$n registereename> as an employee
                    else
                        mpecho ^p$i pings ^w'ERROR: Registeree not found in the room.'^N
                    endif
            else
                    mpecho ^p$i pings ^w'ERROR: Unauthorized ID.'^N
            endif
        else
            mpecho ^p$i pings ^w'ERROR: Invalid room ownership.'^N
        endif
    endif
else
    mpechoat $n $i is not turned on!
endif
return cancel
~

## removes an employee UNREGISTER EMPLOYEE
cnclmsg_prog huh p unregister employee
if var($i powerswitch == 1)
    if islike($n -LOCATION +OWNED)
        mpprompt $n registereename Name of the Employee to remove
        if cansee($i '$<$n registereename>')
            mptattoo $i '-$<$n registereename>'
            mpecho $n types in some things to the terminal and unregisters $<$n registereename> as an employee
        else
            mpecho ^p$i pings ^w'ERROR: Registeree not found in the room.'^N
        endif
    else
        if islike($n -LOCATION +CLANOWNED)
            if clanrank($n == 6)
                mpprompt $n registereename Name of the Employee
                if cansee($i '$<$n registereename>')
                    mptattoo $i '-$<$n registereename>'
                    mpecho $n types in some things to the terminal and unregisters $<$n registereename> as an employee
                else
                    mpecho ^p$i pings ^w'ERROR: Registeree not found in the room.'^N
                endif
            else
                mpecho ^p$i pings ^w'ERROR: Unauthorized ID.'^N
            endif
        else
            mpecho ^p$i pings ^w'ERROR: Invalid room ownership.'^N
        endif
    endif
else
    mpechoat $n $i is not turned on!
endif
return cancel
~

cnclmsg_prog huh p resign
if var($i powerswitch == 1)
if islike($i '-TATTOOS +$n')
mptattoo $i '-$n'
mpecho $n types in some things to the terminal and resigns from the job.
else
mpechoat $n $i is not turned on!
endif
return cancel
~

cnclmsg_prog huh p Balance
if var($i powerswitch == 1)
mpecho $n types in some things to the terminal and pulls up the balance.
mpalarm 1 mpecho ^p$i pings ^w'Currently available funds: ^g$%GOLDAMT($i)%c^w'^N
if goldamt($i < 5000)
mpalarm 1 mpecho ^p$i pings ^w'Available funds might be insufficient to resume operations, please alert management if the balance is too low.'^N
else
endif
else
mpechoat $n $i is not turned on!
endif
return cancel
~
